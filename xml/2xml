#!/usr/bin/awk -f

# universal tabular-to-xml converter
# requires meta-field 'prog:[progname]' 

BEGIN { 
  head=0; 
  prog="unknown" 
  printf("<?xml version=\"1.0\" encoding=\"ISO-8859-1\" ?>\n")
  printf("<cof\n  xmlns=\"http://smutek.pl/cof\"\n  xmlns:xsi=\"http://www.w3.org/2001/XMLSchema-instance\"\n  xsi:schemaLocation=\"http://smutek.pl.cof cof.xsd\">\n");
}

/^#/  { 
  if ( $2 == "meta") {
    for (i=1; i<=NF; i++) {
      split($i,a,":")
      if (a[1]=="prog")  { prog=a[2] }
    }
  }
  head = 0  # any 'comment' field resets header marker
  next
}

head==0  {
  delete f
  for (i=1; i<=NF; i++) { f[i] = $i }
  head=1
  next
} 

{
  printf("  <%s_entry>\n", prog)
  for (i=1; i<=NF; i++) { 
    gsub("\"","\\&quot;", $i) 
    gsub("'", "\\&apos;", $i) 
    gsub("&", "\\&amp;",  $i) 
    gsub("<", "\\&lt;",   $i) 
    gsub(">", "\\&gt;",   $i)
    printf("    <%s>%s</%s>\n", f[i], $i, f[i]) 
  }
  printf("  </%s_entry>\n", prog)
}

END   { printf("</cof>\n") }

# eof

