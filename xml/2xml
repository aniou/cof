#!/usr/bin/awk -f

# universal tabular-to-xml converter
# requires meta-field 'prog:[progname]' 

BEGIN { 
    head=0; opentag=0; prog="unknown" 
}

/^#/  { 
  for (i=1; i<=NF; i++) {
    split($i,a,":")
    if (a[1]=="prog")  { n=split(a[2],b,"/"); prog=b[n] }
  }
  next
}

opentag==0 { 
  printf("<?xml version=\"1.0\" encoding=\"ISO-8859-1\" ?>\n")
  printf("<!DOCTYPE %s_list SYSTEM \"cof.dtd\">\n", prog )

  printf("<%s_list>\n", prog)
  opentag=1
}

head==0  {
    for (i=1; i<=NF; i++) { f[i] = $i }
    head=1
    next
} 

{
  printf("  <%s_entry>\n", prog)
  for (i=1; i<=NF; i++) { 
    gsub("\"","\\&quot;", $i) 
    gsub("'", "\\&apos;", $i) 
    gsub("&", "\\&amp;",  $i) 
    gsub("<", "\\&lt;",   $i) 
    gsub(">", "\\&gt;",   $i)
    printf("    <%s>%s</%s>\n", f[i], $i, f[i]) 
  }
  printf("  </%s_entry>\n", prog)
}

END   { printf("</%s_list>\n", prog) }

# eof

